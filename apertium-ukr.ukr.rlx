DELIMITERS = "<.>" "<!>" "<?>" ; 
SOFT-DELIMITERS = "<,>" ;

# =========================================================================== #

LIST BOS = (>>>) ; 
LIST EOS = (<<<) (".") ("..") ("...");

# Tags

LIST N = n ;
LIST Prop = np ;
LIST A = adj ;
LIST Pr = pr ;
LIST Det = det ;
LIST Interj = ij ;
LIST Interr = ij ;
LIST Adv = adv ;
LIST Num = num ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Pron = prn;
LIST V = vblex vbser vbmod vaux  ;

LIST Nom = nom ;
LIST Gen = gen ;
LIST Dat = dat ;
LIST Acc = acc ;
LIST Ins = ins ;
LIST Loc = loc ;
LIST Voc = voc ;

LIST Sg = sg ;
LIST Pl = pl ;

LIST Fem = f ;
LIST Masc = m ;
LIST Neu = nt ; 

LIST Sg3 = (p3 sg);
LIST Pl3 = (p3 pl);

LIST 3p = (p3);

LIST Past = past ;
LIST Pres = pres ;
LIST Fut = fut ;

LIST TV = tv ; 
LIST IV = iv ; 

LIST CASE = nom gen dat acc ins loc voc ;

LIST GENDER = m f nt mf mfn ;
LIST NUMBER = sg pl sp ;
LIST ANIMAC = aa nn an ;

LIST ADV-NEG = "ніде" ;

LIST ADV-GEN = "найбільше" ; 

LIST BUTI = "бути";

LIST PREP-GEN = "без" "біля" "близько" "від" "впродовж" "у" "для" "до" "за" "з" "замість" "коло" "крім" "навколо" "після" "поблизу" "посеред" "проти" "протягом" "серед" "щодо" ;
LIST PREP-DAT = "завдяки" "всупереч" "наперекір" ;
LIST PREP-ACC = "у" "за" "крізь" "між" "на" "над" "перед" "під" "по" "поза" "поміж" "понад" "попід" "попри" "про" "через" ; 
LIST PREP-INS = "з" "за" "між" "над" "перед" "під" "поза" "поміж" "понад" "попід" "протягом" ;
LIST PREP-LOC = "у" "о" "на" "по" "при" ;

LIST V-FIN = pres past fut imp ;
#SET V-FIN = Pres | Past | Fut ; 
LIST V-FIN-PAST = Past ; 

SET Pers = (p1) | (p2) | (p3) ;

SET NounMascFem = (n m) OR (n f) OR (n mfn) ;
SET NounMascNeu = (n m) OR (n nt) OR (n mfn) ;
SET NounFemNeu = (n f) OR (n nt) OR (n mfn) ;

SET NOM-OR-GEN = Nom | Gen ; 
SET NOM-OR-ACC = Nom | Acc ;
SET ACC-OR-GEN = Acc | Gen ;

SET NOTMSC = Fem | Neu ;
SET NOTFEM = Masc | Neu ;
SET NOTNEU = Masc | Fem | (mf) ;

SET NOTNOM = Gen | Dat | Acc | Ins | Loc | Voc ;
SET NOTGEN = Nom | Dat | Acc | Ins | Loc | Voc ;
SET NOTDAT = Nom | Gen | Acc | Ins | Loc | Voc ;
SET NOTACC = Nom | Gen | Dat | Ins | Loc | Voc ;
SET NOTINS = Nom | Gen | Dat | Acc | Loc | Voc ;
SET NOTLOC = Nom | Gen | Dat | Acc | Ins | Voc ;
SET NOTVOC = Nom | Gen | Dat | Acc | Ins | Loc ;

SET NOTACCINS = Nom | Gen | Dat | Loc | Voc ; 
SET NOTNOMACC = Ins | Gen | Dat | Loc | Voc ; 
SET NOTNOMGEN = Ins | Acc | Dat | Loc | Voc ; 
SET NOTNOMVOC = Gen | Dat | Acc | Ins | Loc ; 

SET OBL    = CASE - Nom ;

LIST DemPron = (prn dem) ;
LIST PosPron = (prn pos) ;
LIST IndPron = (prn ind) ;

# Boundary sets 

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

LIST Nomen = top al ant cog n np prn adj det; # Words that have gender, number and case

SET PRE-N =  A | Det | Num | (n gen) | (prn gen) | CC ; # Det???

SET Modifier = A | Det | Num ; # Words that agree in case, number, gender with the following word.
    # (differs from PRE-N in that it always agrees with the headword)
    # | DemPron | PosPron | IndPron removed

LIST COMMA = (",") (cm) ;

LIST CMSEMIC = "," ";" ;
SET MARK =  COMMA | ("\\") | ("\;") ; #"

SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

SET NP-MEMBER = PRE-N | N | Pron ;

SET NP-HEAD = N | Pron | Prop ;

SET S-BOUNDARY = CS | Interr | (";") ;

LIST VERB-DAT = "казати"i "відповідати"i "купувати"i "посміхатися"i "ходити"i ; # TODO: complete the list; "ходити" - CHECK

# =========================================================================== #

SECTION

REMOVE CS IF (0 ("что")) (NOT -1 COMMA) (NOT -1 BOS) ;
    ## Вот что говорят об этом исторические архивы. 

SELECT CS IF (0 ("что")) (-1 COMMA) ;
    ## Практика показывает, что для её решения уже недостаточно существующих физических теорий.

REMOVE Det + $$CASE IF (NOT 1* $$CASE BARRIER N) ;
    ## вона дивиться у вікно і бачить Іванка та Марічку, що бавляться.
    ## она смотрит в окно и видит Ваню и Машу, что играют.

SELECT Gen IF (0* ("бути"i) + (neg) BARRIER BOS | EOS | CMSEMIC)
              (NOT 0* V-FIN BARRIER BOS | EOS | CMSEMIC) ;
    ## Вона заглядає під стіл і під крісла, але Іванка ніде немає.
    ## Она заглядывает под стол и под стулья, но Вани нигде нет.

# Preposition based case disambiguation:
REMOVE:Case Pr + $$CASE IF (1C Nomen - $$CASE)
REMOVE:Case Nomen + $$CASE IF (-1C Pr - $$CASE)
# Modifier based case, number disambiguation:
REMOVE:Case Nomen + $$CASE IF (-1C Modifier - $$CASE)
REMOVE:Number Nomen + $$NUMBER IF (-1C Modifier - $$NUMBER)
    ## в садку. у садку. у вікно. в хату. навпроти великого будинку. на своє питання
REMOVE:Case Pr + $$CASE IF (1C Modifier - $$CASE)   # CHECK

REMOVE:Locative Nomen + Loc IF (0 Dat OR Loc) (NOT -1 Pr + Loc) (NOT -1C Modifier + Loc);

#SELECT Gen IF (-1* ("всередині") | ("навпроти") BARRIER NOTGEN) ;
    ## вони завжди бавляться у садку навпроти великого будинку.
    ## они всегда играют в саду напротив большого дома.

REMOVE:Dative Dat IF (NOT 0* VERB-DAT) ;
    ## А ви знаєте чому? Чому вона це робить?

SELECT Sg3 IF (0 BUTI) (-1 Pron + Gen LINK -1 ("у"));
    ## У нас є дуже чітка мета – це дбати за особливі місця для усіх.

SELECT ("рік") IF (0 ("рок") OR ("рік")) ;
    ## 2015 року Національному фонду виповниться 120 років.

SELECT $$CASE IF (0C A) (0 A + $$CASE + $$NUMBER) (1C N + $$CASE + $$NUMBER);

SELECT $$CASE IF (0C A) (0 A + $$CASE + $$NUMBER) (1 A + $$CASE + $$NUMBER) (2C N + $$CASE + $$NUMBER);

# This is really fragile
REMOVE Gen IF (0 ("у")) (NOT 1 Pron + Gen);
   ## Виконавчий директор організації Гелен Ґош в інтерв’ю британським ЗМІ після призначення наголошувала на зв’язках з громадськістю.

SELECT Gen IF (-1C ADV-GEN) (0C Gen OR Acc); 
   ## Найбільше таких коментаторів в Києві.

SELECT Gen IF (-1C PREP-GEN) (0C Gen);
   ## На думку Стецьківа, проведення референдуму щодо змін до Конституції має сенс тільки після формування нового складу Ради.

SELECT Acc IF (-1C PREP-ACC) (0C Acc);
   ## На думку фахівців, новий грип перетворився на звичайне сезонне захворювання.

REMOVE Pr (1 EOS) ;
   ## На думку Стецьківа, проведення референдуму щодо змін до Конституції має сенс тільки після формування нового складу Ради.

REMOVE Loc IF (0C Pr) (NOT 1 Loc);
   ## На думку фахівців, новий грип перетворився на звичайне сезонне захворювання.

REMOVE Acc IF (0C Pr) (NOT 1 Acc);
   ## На думку фахівців, новий грип перетворився на звичайне сезонне захворювання.

SELECT A IF (0C A + $$CASE OR Adv) (1 A + $$CASE OR N + $$CASE) ; 

AFTER-SECTIONS

REMOVE:h001 Voc IF (0 Nom OR Acc OR Voc);
SELECT:h002 Acc IF (-1 TV) (0C Nom OR Acc);
SELECT:h003 Nom IF (NOT -1 TV) (0C Nom OR Acc);
SELECT:h004 $$CASE + $$GENDER (-1 $$CASE + $$GENDER);
SELECT:h005 $$CASE + $$GENDER (1C $$CASE + $$GENDER);

SELECT:h006 3p IF (0C BUTI LINK 0 Pres) ; 
   ## На думку фахівців, знайдені боєприпаси є фугасною авіаційною бомбою німецького виробництва.
